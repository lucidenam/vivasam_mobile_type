USE [vivasam_mobile]
GO

/****** Object:  StoredProcedure [dbo].[SP_SAEMTEO_CULTURE_OFFLINE_MERGE]    Script Date: 2019-01-10 오후 6:00:44 ******/
SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO

-- =============================================
-- Author:		심원보
-- Create date: 2016-09-08
-- Description:	비바샘 관리자 > 비바샘터> 교사문화 프로그램/오프라인 세미나 등록/수정/삭제
-- EXEC SP_SAEMTEO_CULTURE_OFFLINE_MERGE  '39' , 'qqqqq' , '2016-07-12' , '2016-07-16' , '인쇄설정 ' , '' , '1' , '1', 'shimwb', 'U'
-- =============================================
ALTER PROCEDURE [dbo].[SP_SAEMTEO_CULTURE_OFFLINE_MERGE]
	@P_CULTURE_ACT_ID			INT = 0,
	@P_TITLE					NVARCHAR(200) = NULL,
	@P_ACT_START_DT				VARCHAR(10) = NULL,
	@P_ACT_END_DT				VARCHAR(10) = NULL,
	@P_CONTENTS					VARCHAR(MAX) = NULL,
	@P_POSTSCRIPT				VARCHAR(MAX) = NULL,
	@P_DATA_STATE_CD			CHAR(1) = NULL,	
	@P_PROGRAM_GUBUN_CD			CHAR(1) = NULL,
	@P_TARGET_PLATFORM			VARCHAR(10) = NULL,
	@P_MOBILE_TITLE				NVARCHAR(200) = NULL,
	@P_MOBILE_CONTENTS			VARCHAR(MAX) = NULL,
	@P_MOBILE_APPLY_CONTENTS	VARCHAR(300) = NULL,
	@P_MOBILE_TERM				VARCHAR(300) = NULL,
	@P_ADD_CHECKBOX_TEXT		VARCHAR(300) = NULL,
	@P_ADD_CHECKBOX_YN			CHAR(1) = NULL,
	@P_MOBILE_THUM_PATH_URL		NVARCHAR(300) = NULL,
	@P_ADM_ID					VARCHAR(30) = NULL,
	@P_MODE						VARCHAR(10) = NULL,
	@RESULT						INT		OUTPUT
AS
BEGIN
	DECLARE @TMP_INFO TABLE(ACTION_TP VARCHAR(10), CULTURE_ACT_ID INT);
	
	BEGIN TRY;
		
		WITH TEMP_CULTURE_ACT_INFO (CULTURE_ACT_ID,TITLE,ACT_START_DT,ACT_END_DT,
			CONTENTS,POSTSCRIPT,DATA_STATE_CD,ADM_ID,PROGRAM_GUBUN_CD, TARGET_PLATFORM, 
			MOBILE_TITLE, MOBILE_CONTENTS, MOBILE_APPLY_CONTENTS, MOBILE_TERM, ADD_CHECKBOX_TEXT, ADD_CHECKBOX_YN, MOBILE_THUM_PATH_URL,
			MODE) AS (
			SELECT
				@P_CULTURE_ACT_ID,
				@P_TITLE,
				@P_ACT_START_DT,
				@P_ACT_END_DT,
				@P_CONTENTS,				
				@P_POSTSCRIPT,
				@P_DATA_STATE_CD,
				@P_ADM_ID,
				@P_PROGRAM_GUBUN_CD,
				@P_TARGET_PLATFORM,
				@P_MOBILE_TITLE,
				@P_MOBILE_CONTENTS,
				@P_MOBILE_APPLY_CONTENTS,
				@P_MOBILE_TERM,
				@P_ADD_CHECKBOX_TEXT,
				@P_ADD_CHECKBOX_YN,
				@P_MOBILE_THUM_PATH_URL,
				@P_MODE
		)
		MERGE dbo.CULTURE_ACT_INFO AS C
		USING (
			SELECT 
				T.CULTURE_ACT_ID,T.TITLE,T.ACT_START_DT,T.ACT_END_DT,
				T.CONTENTS, T.POSTSCRIPT, CAI.CONTENTS AS OLD_CONTENTS, CAI.POSTSCRIPT AS OLD_POSTSCRIPT,
				T.DATA_STATE_CD,T.ADM_ID, T.PROGRAM_GUBUN_CD, T.TARGET_PLATFORM, 
				T.MOBILE_TITLE, T.MOBILE_CONTENTS, T.MOBILE_APPLY_CONTENTS, T.MOBILE_TERM, T.ADD_CHECKBOX_TEXT, T.ADD_CHECKBOX_YN, T.MOBILE_THUM_PATH_URL, 
				T.MODE 
			FROM TEMP_CULTURE_ACT_INFO AS T
			LEFT JOIN dbo.CULTURE_ACT_INFO AS CAI ON T.CULTURE_ACT_ID = CAI.CULTURE_ACT_ID AND CAI.CULTURE_ACT_ID = @P_CULTURE_ACT_ID
		) AS T ON C.CULTURE_ACT_ID = T.CULTURE_ACT_ID
		WHEN NOT MATCHED AND T.MODE = 'I' THEN
			INSERT 
				(TITLE,
				ACT_START_DT,
				ACT_END_DT,
				CONTENTS,
				DATA_STATE_CD,
				PROGRAM_GUBUN_CD,
				TARGET_PLATFORM,
				MOBILE_TITLE,
				MOBILE_CONTENTS,
				MOBILE_APPLY_CONTENTS, 
				MOBILE_TERM, 
				ADD_CHECKBOX_TEXT, 
				ADD_CHECKBOX_YN, 
				MOBILE_THUM_PATH_URL,
				REG_DTTM,
				REGR_ID)
			 VALUES (
				T.TITLE
				,T.ACT_START_DT
				,T.ACT_END_DT
				,T.CONTENTS
				,T.DATA_STATE_CD
				,T.PROGRAM_GUBUN_CD
				,T.TARGET_PLATFORM
				,T.MOBILE_TITLE
				,T.MOBILE_CONTENTS
				,T.MOBILE_APPLY_CONTENTS
				,T.MOBILE_TERM
				,T.ADD_CHECKBOX_TEXT
				,T.ADD_CHECKBOX_YN
				,T.MOBILE_THUM_PATH_URL
				,GETDATE()
				,T.ADM_ID)
		WHEN MATCHED AND T.MODE = 'U' THEN
			UPDATE 
			SET 
			  C.TITLE = T.TITLE
			  ,C.ACT_START_DT = T.ACT_START_DT
			  ,C.ACT_END_DT = T.ACT_END_DT
			  ,C.CONTENTS = CASE WHEN ISNULL(T.CONTENTS, '') = '' THEN T.OLD_CONTENTS ELSE T.CONTENTS END
			  ,C.POSTSCRIPT = CASE WHEN ISNULL(T.POSTSCRIPT, '') = '' THEN T.OLD_POSTSCRIPT ELSE T.POSTSCRIPT END
			  ,C.POSTSCRIPT_GUBUN_CD = CASE WHEN ISNULL(T.POSTSCRIPT, '') != '' OR DATALENGTH(T.OLD_POSTSCRIPT) > 0 THEN '1' ELSE NULL END
			  ,C.DATA_STATE_CD = T.DATA_STATE_CD
			  ,C.TARGET_PLATFORM = T.TARGET_PLATFORM
			  ,C.MOBILE_TITLE = T.MOBILE_TITLE
			  ,C.MOBILE_CONTENTS = T.MOBILE_CONTENTS
			  ,C.MOBILE_THUM_PATH_URL = T.MOBILE_THUM_PATH_URL
			  ,C.MOBILE_APPLY_CONTENTS = T.MOBILE_APPLY_CONTENTS
			  ,C.MOBILE_TERM = T.MOBILE_TERM
			  ,C.ADD_CHECKBOX_TEXT = T.ADD_CHECKBOX_TEXT
			  ,C.ADD_CHECKBOX_YN = T.ADD_CHECKBOX_YN
			  ,C.UPT_DTTM = GETDATE()
			  ,C.UPTR_ID = T.ADM_ID			  
		WHEN MATCHED AND T.MODE = 'D' THEN
			DELETE			
		OUTPUT $ACTION, T.CULTURE_ACT_ID INTO @TMP_INFO; --삭제 요청시에만 참여자 정보도 삭제하기 위해 요청된 ID를 저장함.
		
		SET @RESULT = @@ROWCOUNT
		
		
		--문화 프로그램/세미나 참여자 정보 삭제		
		IF (SELECT COUNT(1) FROM @TMP_INFO WHERE ACTION_TP = 'DELETE') > 0
		BEGIN
			DELETE FROM dbo.CULTURE_ACT_APPLY_INFO 
			WHERE CULTURE_ACT_ID IN (SELECT CULTURE_ACT_ID FROM @TMP_INFO WHERE ACTION_TP = 'DELETE');
		END
		
	END TRY
	BEGIN CATCH
		SET @RESULT = 0
		
		EXEC dbo.SP_GETERRORINFO
	END CATCH;
	
END






GO

